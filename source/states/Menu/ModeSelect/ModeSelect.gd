extends Node2D

const StoryMenu = preload("uid://dlwh1vofi13a5")
const Freeplay = preload("uid://c5emumn8mkcd5")
const Options = preload("uid://bf7b2xysqcv0i")
const OptionScroll = preload("uid://d3jka7l4iy07n")

#region Editors
const CharacterEditorScene = preload("uid://droixhbemd0xd")

const ChartEditorScene = preload("uid://bw5vas6axpdqk")

const menu_options_name: PackedStringArray = ['story_mode','freeplay','mods','options']
const mods_options: PackedStringArray = ['Character Editor','Chart Editor','Modchart Editor', "Mod Creator"]

var bg: Sprite2D = Sprite2D.new()

var menu_option_nodes: Dictionary

var options: Array = []
var canSwap: bool = true

var _is_blinking: bool = false

var treeSwap: Timer = Timer.new()

@onready var option_parent: OptionScroll = OptionScroll.new()
@onready var mods_parent: OptionScroll = preload("uid://d3jka7l4iy07n").new()
@onready var cur_tab: OptionScroll = option_parent

@onready var version: Label = Label.new()
var tab_tweens: Dictionary[OptionScroll,Tween]

var return_tabs: Array[OptionScroll]

func spawn():
	do_tab_tween(option_parent,{^'modulate:a': 1.0},0.5,true)
	create_tween().tween_property(self,'modulate',Color.WHITE,0.5)
	set_process_input(true)
	canSwap = true

func transparent():
	do_tab_tween(option_parent,{^'modulate:a': 0.0},0.5,true)
	stop_blink()
	create_tween().tween_property(self,'modulate',Color.DIM_GRAY,0.5)

func blink() -> void: _is_blinking = true

func stop_blink() -> void:
	_is_blinking = false
	canSwap = true
	bg.modulate = Color.WHITE
	if cur_tab.option_node: cur_tab.option_node.visible = true


func _update_bg_scale():
	var val = ScreenUtils.screenSize/ScreenUtils.defaultSize
	var val_max = maxf(val.x,val.y)
	bg.scale = Vector2(val_max,val_max)
	bg.position = ScreenUtils.screenSize * 0.5
	version.position.y = ScreenUtils.screenHeight-50

func _ready():
	bg.texture = Paths.texture('menuBG')
	bg.centered = true
	
	_update_bg_scale()
	get_window().size_changed.connect(_update_bg_scale)
	add_child(bg)
	
	treeSwap.name = &'treeSwap'
	treeSwap.timeout.connect(func():
		stop_blink(); set_process_input(false); exitTo(cur_tab.option_node)
	)
	add_child(treeSwap)
	
	FunkinGD.playSound(Paths.music('freakyMenu'),1.0,'freakyMenu',false,true)
	
	loadModeSelectOptions()
	
	_create_version()

func _create_version():
	version.label_settings = LabelSettings.new()
	version.label_settings.outline_size = 6
	version.label_settings.outline_color = Color.BLACK
	version.vertical_alignment = VERTICAL_ALIGNMENT_TOP
	version.text = 'FNF: Godot Engine v'+ProjectSettings.get_setting("application/config/version")+'\nby n_Myt'
	
	add_child(version)
	

func loadModeSelectOptions():
	option_parent.name = &'Options'
	
	
	var menu_data = getMenuBaseData()
	menu_data.merge(Paths.loadJson('mainmenu/menu'),true)
	option_parent.camera_limit_y = menu_data.camera_limit_y
	for menus in menu_options_name:
		var menu_pos = menu_data.get(menus+'_position',[0,0])
		var menu: FunkinAnimatedSprite2D = FunkinAnimatedSprite2D.new('mainmenu/menu_'+menus)
		menu.name = menus
		menu.modulate = OptionScroll.UNSELECTED_COLOR
		menu.animation.add_animation_by_prefix(&'static',menus+' basic',24,true)
		menu.animation.add_animation_by_prefix(&'selected',menus+' white',24,true)
		menu.animation.add_animation_offset(&"static",Vector2.ZERO)
		menu.animation.add_animation_offset(&"selected", menu.pivot_offset/3.0)
		
		menu.offset_follow_scale = true
		menu.position = Vector2(menu_pos[0] - menu.pivot_offset.x,menu_pos[1]) - ScreenUtils.screenOffset*0.5
		option_parent.add_child(menu)
		option_parent.options.append(menu)
		options.append(menu)
		menu_option_nodes['menu_'+menus] = menu
	
	option_parent.scrolled.connect(func(i,_prev_i):
		option_parent.options[_prev_i].animation.play(&'static')
		option_parent.options[i].animation.play(&'selected')
	)
	add_child(option_parent)


func _process(_d) -> void:
	if _is_blinking:
		var time: int = int(Time.get_ticks_usec())/40000
		cur_tab.option_node.visible = bool(time%3)
		bg.modulate = Color.WHITE if not bool(time%6) else Color.MEDIUM_PURPLE

func set_option(index: int = cur_tab.option_index):
	if not canSwap: return
	var optionSize = cur_tab.options.size()-1
	if index > optionSize: index = 0
	elif index < 0:index = optionSize
	cur_tab.option_index = index
	FunkinGD.playSound(&'scrollMenu')

#region Tabs
func select_tab(tab: OptionScroll):
	do_tab_tween(cur_tab,{^'modulate:a': 0.5,^'scale': Vector2(0.8,0.8)},1.0,true)
	return_tabs.append(cur_tab)
	
	var index: int = 1
	for i in return_tabs:
		i.create_tween().tween_property(i,^'position:x',-300*index,1.0).set_trans(Tween.TRANS_CUBIC)
		index += 1
	cur_tab = tab
	cur_tab.position.x = 0.0
	cur_tab.scale = Vector2.ONE
	do_tab_tween(tab,{^'modulate:a': 1.0,&'scale': Vector2.ONE},0.8,true)

func return_tab():
	if !return_tabs: return
	
	do_tab_tween(cur_tab,{^'modulate:a': 0.0},0.3,true)
	
	cur_tab = return_tabs.pop_back()
	do_tab_tween(cur_tab,{^'position:x': 0.0,^'modulate:a': 1.0,&'scale': Vector2.ONE},0.8,true)
	var index: int = return_tabs.size()
	for i in return_tabs:
		do_tab_tween(i,{^'position:x': -200*index},0.8,true)
		index -= 1
	
func do_tab_tween(tab: OptionScroll, properties: Dictionary, duration: float, kill: bool = false):
	var tween: Tween = tab_tweens.get(tab)
	if tween and kill: tween.stop(); tween = null
	if !tween: 
		tween = tab.create_tween().set_trans(Tween.TRANS_CUBIC).set_ease(Tween.EASE_IN_OUT)
		tween.set_parallel(true)
		tab_tweens[tab] = tween
		tween.finished.connect(func(): tab_tweens.erase(tab))
	
	for i in properties: tween.tween_property(tab,str(i),properties[i],duration)
	return tween
#endregion

func selectOption(node: Node = cur_tab.option_node):
	if cur_tab == option_parent:
		canSwap = false
		FunkinGD.playSound(&'confirmMenu')
		blink()
		treeSwap.start(1)
	else: exitTo(node)

func exitTo(option_node: Node):
	treeSwap.stop()
	stop_blink()
	if cur_tab == option_parent:
		match option_node.name:
			&'story_mode':
				var story_menu = StoryMenu.new()
				story_menu.back_to = get_script()
				SceneManager.change_scene(story_menu)
			&'freeplay':
				var node = Freeplay.new()
				Freeplay.back_to = get_script()
				SceneManager.change_scene(node)
			&'mods':
				select_tab(mods_parent)
				set_process_input(true)
			&'options':
				var i = Options.new()
				i.back_to = get_script()
				SceneManager.change_scene(i)
			_:
				set_process_input(true)
		return
	
	if cur_tab == mods_parent:
		match option_node.name:
			&'Character Editor': 
				SceneManager.change_scene(CharacterEditorScene)
				CharacterEditorScene.get_state().get_node_property_value(0,0).back_to = get_script()
			&'Chart Editor': 
				SceneManager.change_scene(ChartEditorScene)

func _input(event):
	if event is InputEventKey and event.pressed:
		match event.keycode:
			KEY_UP: set_option(cur_tab.option_index - 1)
			KEY_DOWN: set_option(cur_tab.option_index + 1)
			KEY_ENTER: selectOption()
			KEY_BACKSPACE:
				FunkinGD.playSound('cancelMenu')
				if _is_blinking:
					stop_blink()
					treeSwap.stop()
				else: return_tab()
				
	elif event is InputEventMouseButton and event.pressed and event.button_index == 1:
		var index: int = 0
		for i in options:
			if MathUtils.is_pos_in_area(event.position,i.global_position,i.image.region_rect.size):
				if index == cur_tab.option_index: selectOption(i)
				else: set_option(index)
				break
			index += 1

static func getMenuBaseData() -> Dictionary:
	return {
		"story_mode_position": [640,50],
		"freeplay_position": [640,225],
		"mods_position": [640,400],
		"options_position": [640,575],
		"camera_limit_y": 100
	}



	
	
